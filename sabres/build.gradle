apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

version = VERSION_NAME
group = GROUP

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 22
        versionCode 1
        //noinspection GroovyAssignabilityCheck
        versionName VERSION_NAME
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            //noinspection GroovyAssignabilityCheck
            project {
                packaging 'aar'
                //noinspection GroovyAssignabilityCheck
                name NAME
                //noinspection GroovyAssignabilityCheck
                description DESCRIPTION
                url SITE_URL

                licenses {
                    license {
                        //noinspection GroovyAssignabilityCheck
                        name LICENCE_NAME
                        url LICENCE_URL
                    }
                }
                developers {
                    developer {
                        id DEVELOPER_ID
                        //noinspection GroovyAssignabilityCheck
                        name DEVELOPER_NAME
                        email DEVELOPER_EMAIL
                    }
                }
                scm {
                    connection GIT_URL
                    developerConnection GIT_URL
                    url SITE_URL
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    //noinspection GroovyAssignabilityCheck
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    //noinspection GroovyAssignabilityCheck
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = BINTRAY_USER
    key = BINTRAY_API_KEY

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = NAME
        desc = DESCRIPTION
        websiteUrl = SITE_URL
        vcsUrl = GIT_URL
        issueTrackerUrl = ISSUE_TRACKER_URL
        licenses = ['Apache-2.0']
        labels = ['Android', 'ORM', 'Sqlite']
        publicDownloadNumbers = true
        publish = true
    }
}

dependencies {
    compile 'com.parse.bolts:bolts-android:1.2.0'
    compile 'com.jakewharton.fliptables:fliptables:1.0.2'
}
